{-# OPTIONS --type-in-type #-}
-- | Monoidal Products and Co-Products on Poly
module Poly.Monoidal where

--------------------------------------------------------------------------------

open import Data.Product using (_Ã—_; _,_; âˆƒ-syntax; Î£-syntax; projâ‚; projâ‚‚)
open import Data.Sum using (_âŠ_; [_,_]; injâ‚; injâ‚‚)
open import Function using (_âˆ˜_; id)
open import Poly
open import Poly.SetFunctor

open _â‰ƒ_

--------------------------------------------------------------------------------

-- | P + Q
-- The Categorical Co-Product of two Polyonomials
--
-- P + Q â‰” âˆ‘[ j âˆˆ I ] x^aáµ¢ + âˆ‘[ j âˆˆ J ] y^bâ±¼
infixr 6 _+_
_+_ : Poly â†’ Poly â†’ Poly
(P + Q) .Base = P .Base âŠ Q .Base
(P + Q) .Fiber (injâ‚ x) = P .Fiber x
(P + Q) .Fiber (injâ‚‚ y) = Q .Fiber y

_+â‡’_ : âˆ€{P Q R Z : Poly} â†’ P â‡’ Q â†’ R â‡’ Z â†’ P + R â‡’ Q + Z
(pâ‡’q +â‡’ râ‡’z) .map-base (injâ‚ ptag) = injâ‚ (map-base pâ‡’q ptag)
(pâ‡’q +â‡’ râ‡’z) .map-base (injâ‚‚ rtag) = injâ‚‚ (map-base râ‡’z rtag)
(pâ‡’q +â‡’ râ‡’z) .map-fiber (injâ‚ ptag) = map-fiber pâ‡’q ptag
(pâ‡’q +â‡’ râ‡’z) .map-fiber (injâ‚‚ rtag) = map-fiber râ‡’z rtag

mergeâ‚š : âˆ€{P : Poly} â†’ P + P â‡’ P
mergeâ‚š .map-base (injâ‚ ptag) = ptag
mergeâ‚š .map-base (injâ‚‚ ptag) = ptag
mergeâ‚š .map-fiber (injâ‚ ptag) pargs = pargs
mergeâ‚š .map-fiber (injâ‚‚ ptag) pargs = pargs

-- | Co-Product Left Inclusion
leftâ‚š : âˆ€{P Q : Poly} â†’ P â‡’ (P + Q) 
leftâ‚š .map-tag = injâ‚
leftâ‚š .map-args tag = id

-- | Co-Product Right Inclusion
rightâ‚š : âˆ€{P Q : Poly} â†’ Q â‡’ (P + Q)
rightâ‚š .map-tag = injâ‚‚
rightâ‚š .map-args tag = id

-- | Co-Product eliminator
eitherâ‚š : âˆ€{P Q R : Poly} â†’ P â‡’ R â†’ Q â‡’ R â†’ (P + Q) â‡’ R
eitherâ‚š pâ‡’r qâ‡’r .map-base (injâ‚ ptag) = pâ‡’r .map-base ptag
eitherâ‚š pâ‡’r qâ‡’r .map-base (injâ‚‚ qtag) = qâ‡’r .map-base qtag
eitherâ‚š pâ‡’r qâ‡’r .map-fiber (injâ‚ tag) = pâ‡’r .map-fiber tag
eitherâ‚š pâ‡’r qâ‡’r .map-fiber (injâ‚‚ tag) = qâ‡’r .map-fiber tag

Sum : (I : Set) â†’ (I â†’ Poly) â†’ Poly
Sum I P .Base = âˆƒ[ i ] P i .Base
Sum I P .Fiber (i , ptag) = P i .Fiber ptag

infix 2 Sum
syntax Sum I (Î» i â†’ P) = Î£â‚š[ i âˆˆ I ] P

âŸ¦âŸ§-+ : âˆ€{P Q : Poly} â†’ âŸ¦ P + Q âŸ§ â‰ƒ âŸ¦ P âŸ§ +â‚ âŸ¦ Q âŸ§
âŸ¦âŸ§-+ .to (injâ‚ ptag , x) = injâ‚ (ptag , x)
âŸ¦âŸ§-+ .to (injâ‚‚ qtag , y) = injâ‚‚ (qtag , y)
âŸ¦âŸ§-+ .from (injâ‚ (ptag , x)) = injâ‚ ptag , x
âŸ¦âŸ§-+ .from (injâ‚‚ (qtag , y)) = injâ‚‚ qtag , y

âŸ¦âŸ§-Sum : {I : Set} â†’ {P : I â†’ Poly} â†’ âŸ¦ Î£â‚š[ i âˆˆ I ] (P i) âŸ§ â‰ƒ (Î£â‚[ i âˆˆ I ] âŸ¦ P i âŸ§)
âŸ¦âŸ§-Sum .to ((i , ptag) , f) = i , ptag , f
âŸ¦âŸ§-Sum .from (i , ptag , f) = (i , ptag) , f

--------------------------------------------------------------------------------

-- | P â— Q
-- Composition of Polyonomial Functors
--
-- âŸ¦ P â— Q âŸ§ â‰¡ âŸ¦ P âŸ§ âˆ˜ âŸ¦ Q âŸ§
-- Î£ ? Î  ?   â‰¡ Î£ Î  (Î£ Î )
_â—_ : Poly â†’ Poly â†’ Poly
(P â— Q) .Base = Î£[ ptag âˆˆ P .Base ] (P .Fiber ptag â†’ Q .Base) 
(P â— Q) .Fiber  (ptag , f) =  Î£[ pargs âˆˆ P .Fiber ptag ] Q .Fiber (f pargs)

âŸ¦âŸ§-â— : âˆ€{P Q : Poly} â†’ âŸ¦ P â— Q âŸ§ â‰ƒ âŸ¦ P âŸ§ âˆ˜ âŸ¦ Q âŸ§
âŸ¦âŸ§-â— .to ((ptag , qtag) , f) = ptag , Î» pargs â†’ qtag pargs , Î» qargs â†’ f (pargs , qargs)
âŸ¦âŸ§-â— .from (ptag , f) = (ptag , Î» pargs â†’ projâ‚ (f pargs)) , Î»{ (pargs , qargs) â†’ projâ‚‚ (f pargs) qargs }

id-â—-intro : (P : Poly) â†’ P â‡’ ğ• â— P
id-â—-intro _ .map-base p = tt , Î» _ â†’ p
id-â—-intro _ .map-fiber p (_ , a) = a

â—-id-intro : (P : Poly) â†’ P â‡’ P â— ğ•
â—-id-intro _ .map-base p = p , (Î» _ â†’ tt)
â—-id-intro _ .map-fiber p (a , _) = a

â—-id-elim : (P : Poly) â†’ P â— ğ• â‡’ P
â—-id-elim _ .map-base (p , _) = p
â—-id-elim _ .map-fiber (p , _) a = a , tt

id-â—-elim : (P : Poly) â†’ ğ• â— P  â‡’ P
id-â—-elim _ .map-base (_ , p) = p tt
id-â—-elim _ .map-fiber (_ , p) a = tt , a

xÂ² : {X : Set} â†’ Poly
xÂ² {X} = monomial (Fin 1) (X Ã— X)

xÂ²' : {X : Set} â†’ Poly
Base xÂ²' = Fin 1
Fiber (xÂ²' {X}) = Î» _ â†’ X Ã— X

_ : {X : Set} â†’ xÂ² {X = X} â‰¡ xÂ²' {X = X}
_ = refl

xÂ³ : {X : Set} â†’ Poly
xÂ³ {X} = monomial (Fin 1) (X Ã— X Ã— X)

xÂ²â—xÂ³ : {X : Set} â†’ Poly
xÂ²â—xÂ³ {X} = xÂ² {X} â— xÂ³ {X}

xÂ²â—xÂ³' : {X : Set} â†’ Poly
Base (xÂ²â—xÂ³' {X}) = Î£[ xÂ²-base âˆˆ Fin 1 ] (X Ã— X â†’ Fin 1)
Fiber (xÂ²â—xÂ³' {X}) = Î» where
  (xÂ²-base , f) â†’ Î£[ xÂ²-fib âˆˆ X Ã— X ] (X Ã— X Ã— X)

_ : {X : Set} â†’ xÂ²â—xÂ³ {X} â‰¡ xÂ²â—xÂ³' {X}
_ = refl

--------------------------------------------------------------------------------

-- | P Ã— Q
--
-- The Binary Categorical Product
--
-- Î£[ (i , j) âˆˆ P .Base Ã— Q .Base ] x^(aáµ¢ + bâ±¼)
infixr 7 _Ã—â‚š_
_Ã—â‚š_ : Poly â†’ Poly â†’ Poly
(P Ã—â‚š Q) .Base  =  P .Base Ã— Q .Base
(P Ã—â‚š Q) .Fiber (ptag , qtag) = P .Fiber ptag âŠ Q .Fiber qtag

-- | _Ã—â‚š_ fst eliminator
fst-Ã—â‚š : âˆ€{P Q : Poly} â†’ (P Ã—â‚š Q) â‡’ P
fst-Ã—â‚š .map-base (ptag , qtag) = ptag
fst-Ã—â‚š .map-fiber (ptag , qtag) pargs = injâ‚ pargs

-- | _Ã—â‚š_ snd eliminator
snd-Ã—â‚š : âˆ€{P Q : Poly} â†’ (P Ã—â‚š Q) â‡’ Q
snd-Ã—â‚š .map-base (ptag , qtag) = qtag
snd-Ã—â‚š .map-fiber (ptag , qtag) qargs = injâ‚‚ qargs

swap-Ã—â‚š : âˆ€{P Q : Poly} â†’ (P Ã—â‚š Q) â‡’ (Q Ã—â‚š P)
swap-Ã—â‚š .map-base (ptag , qtag) = qtag , ptag
swap-Ã—â‚š .map-fiber (ptag , qtag) (injâ‚ qargs) = injâ‚‚ qargs
swap-Ã—â‚š .map-fiber (ptag , qtag) (injâ‚‚ pargs) = injâ‚ pargs

dupe-Ã—â‚š : âˆ€{P : Poly} â†’ P â‡’ P Ã—â‚š P
dupe-Ã—â‚š .map-base ptag =  ptag , ptag
dupe-Ã—â‚š .map-fiber ptag (injâ‚ pargs) = pargs
dupe-Ã—â‚š .map-fiber ptag (injâ‚‚ pargs) = pargs

_&&&_ : âˆ€{P Q R : Poly} â†’ R â‡’ P â†’ R â‡’ Q â†’ R â‡’ (P Ã—â‚š Q)
(f &&& g) .map-base rtag =  map-base f rtag , map-base g rtag
(f &&& g) .map-fiber rtag (injâ‚ pargs) = map-fiber f rtag pargs
(f &&& g) .map-fiber rtag (injâ‚‚ qargs) = map-fiber g rtag qargs

âŸ¦âŸ§-Ã—â‚š : âˆ€{P Q : Poly} â†’ âŸ¦ P Ã—â‚š Q âŸ§ â‰ƒ âŸ¦ P âŸ§ Ã—â‚ âŸ¦ Q âŸ§
âŸ¦âŸ§-Ã—â‚š .to ((ptag , qtag) , f) = (ptag , Î» pargs â†’ f (injâ‚ pargs)) , (qtag , Î» qargs â†’ f (injâ‚‚ qargs))
âŸ¦âŸ§-Ã—â‚š .from ((ptag , f) , (qtag , g)) = (ptag , qtag) , [ f , g ]

-- | p Ã—â‚š q can be recovered as Product Bool (if _ then q else p)
Product : (I : Set) â†’ (I â†’ Poly) â†’ Poly
(Product I f) .Base = âˆ€ (i : I) â†’ f i .Base
(Product I f) .Fiber tags = Î£[ i âˆˆ I ] (f i) .Fiber (tags i)

infix 2 Product
syntax Product I (Î» i â†’ P) = Î â‚š[ i âˆˆ I ] P

âŸ¦âŸ§-Product : {I : Set} {P : I â†’ Poly} â†’ âŸ¦ Î â‚š[ i âˆˆ I ] P i âŸ§ â‰ƒ (Î â‚[ i âˆˆ I ] âŸ¦ P i âŸ§)
âŸ¦âŸ§-Product .to (ptag , f) i = ptag i , Î» pargs â†’ f (i , pargs)
âŸ¦âŸ§-Product .from f = projâ‚ âˆ˜ f , Î» (i , pargs) â†’ projâ‚‚ (f i) pargs

--------------------------------------------------------------------------------

-- | P âŠ— Q
-- Also called the Parallel Product of two Polynomials
--
-- P âŠ— Q â‰” âˆ‘[ i âˆˆ P .Base Ã— Q .Base ] y^(aáµ¢ Ã— bâ±¼)
infixr 7 _âŠ—_
_âŠ—_ : Poly â†’ Poly â†’ Poly
(P âŠ— Q) .Base  = Base P Ã— Base Q
(P âŠ— Q) .Fiber (ptag , qtag) = Fiber P ptag Ã— Fiber Q qtag

swap-âŠ— : âˆ€{P Q : Poly} â†’ P âŠ— Q â‡’ Q âŠ— P
swap-âŠ— .map-base (ptag , qtag) = qtag , ptag
swap-âŠ— .map-fiber tag (qargs , pargs) = pargs , qargs

dupe-âŠ— : âˆ€{P : Poly} â†’ P â‡’ P âŠ— P
dupe-âŠ— .map-base ptag =  ptag , ptag
dupe-âŠ— .map-fiber ptag (f , g) = f

-- | The parallel product represents day convolution.
âŸ¦âŸ§-âŠ— : âˆ€{P Q : Poly} â†’ âŸ¦ P âŠ— Q âŸ§ â‰ƒ day âŸ¦ P âŸ§ âŸ¦ Q âŸ§
âŸ¦âŸ§-âŠ— {P = P} {Q = Q} .to ((ptag , qtag) , f) = P .Fiber ptag , Q .Fiber qtag , (Î» pargs qargs â†’ f (pargs , qargs)) , (ptag , id) , (qtag , id)
âŸ¦âŸ§-âŠ— .from (B , C , f , (ptag , b) , (qtag , c)) = (ptag , qtag) , Î» (pargs , qargs) â†’ f (b pargs) (c qargs)

--------------------------------------------------------------------------------

-- | P â“‹ Q
--
-- Î£[ (i , j) âˆˆ P .Base Ã— Q . Base] x^(aáµ¢ â“‹ bâ±¼)
infixr 8 _â“‹_
_â“‹_ : Poly â†’ Poly â†’ Poly
(P â“‹ Q) .Base = P .Base Ã— Q .Base
(P â“‹ Q) .Fiber = Î» where
  (ptag , qtag) â†’  Fiber P ptag âŠ (Fiber P ptag Ã— Fiber Q qtag) âŠ Fiber Q qtag

--------------------------------------------------------------------------------

-- | P âˆ¨ Q
--
-- P âˆ¨ Q â‰” P + (P âŠ— Q) + Q
infixr 5 _âˆ¨_
_âˆ¨_ :  Poly â†’ Poly â†’ Poly
P âˆ¨ Q = P + (P âŠ— Q) + Q

-- | _âˆ¨_ This Inclusion
This : âˆ€{P Q : Poly} â†’ P â‡’ (P âˆ¨ Q)
This .map-base ptag = injâ‚ ptag
This .map-fiber ptag = id

-- | _âˆ¨_ That Inclusion
That : âˆ€{P Q : Poly} â†’ Q â‡’ (P âˆ¨ Q)
That .map-base qtag = injâ‚‚ (injâ‚‚ qtag)
That .map-fiber qtag = id

-- | _âˆ¨_ These Inclusion
These : âˆ€{P Q : Poly} â†’ (P âŠ— Q) â‡’ (P âˆ¨ Q)
These .map-base tags = injâ‚‚ (injâ‚ tags)
These .map-fiber tags = id

-- | _âˆ¨_ Eliminator
theseâ‚š : âˆ€{P Q R : Poly} â†’ P â‡’ R â†’ Q â‡’ R â†’ (P âŠ— Q) â‡’ R â†’ (P âˆ¨ Q) â‡’ R
(theseâ‚š pâ‡’r qâ‡’r pqâ‡’r) .map-base  (injâ‚ ptag) = map-base pâ‡’r ptag
(theseâ‚š pâ‡’r qâ‡’r pqâ‡’r) .map-base (injâ‚‚ (injâ‚ tags)) = map-base pqâ‡’r tags
(theseâ‚š pâ‡’r qâ‡’r pqâ‡’r) .map-base (injâ‚‚ (injâ‚‚ qtag)) = map-base qâ‡’r qtag
(theseâ‚š pâ‡’r qâ‡’r pqâ‡’r) .map-fiber (injâ‚ ptag) args = map-fiber pâ‡’r ptag args
(theseâ‚š pâ‡’r qâ‡’r pqâ‡’r) .map-fiber (injâ‚‚ (injâ‚ tags)) args = map-fiber pqâ‡’r tags args
(theseâ‚š pâ‡’r qâ‡’r pqâ‡’r) .map-fiber (injâ‚‚ (injâ‚‚ qtag)) args = map-fiber qâ‡’r qtag args
--theseâ‚š pâ‡’r qâ‡’r pqâ‡’r = {!!}

--------------------------------------------------------------------------------

-- | P âŠ˜ Q
--
-- P âŠ˜ Q â‰” P + (P Ã—â‚š Q) + Q
infixr 4 _âŠ˜_
_âŠ˜_ : Poly â†’ Poly â†’ Poly
P âŠ˜ Q = P + (P Ã—â‚š Q) + Q

--------------------------------------------------------------------------------

-- | P âŠ› Q
--
-- P âŠ› Q â‰”  P + (P â“‹ Q) + Q
infixr 4 _âŠ›_
_âŠ›_ : Poly â†’ Poly â†’ Poly
P âŠ› Q = P + (P â“‹ Q) + Q

--------------------------------------------------------------------------------

_Ã—â‚šâ‡’_ : âˆ€{P Q R Z : Poly} â†’ P â‡’ Q â†’ R â‡’ Z â†’ P Ã—â‚š R â‡’ Q Ã—â‚š Z
(pâ‡’q Ã—â‚šâ‡’ râ‡’z) .map-base (ptag , rtag) = (map-base pâ‡’q ptag) , (map-base râ‡’z rtag)
(pâ‡’q Ã—â‚šâ‡’ râ‡’z) .map-fiber (ptag , rtag) (injâ‚ qargs) = injâ‚ (map-fiber pâ‡’q ptag qargs)
(pâ‡’q Ã—â‚šâ‡’ râ‡’z) .map-fiber (ptag , rtag) (injâ‚‚ zargs) = injâ‚‚ (map-fiber râ‡’z rtag zargs)

-- | Parallel composition of systems, eg. the Parallel Product of
-- natural transformations between polynomials.
--
--      -------
-- >--A-|-[ ]-|-B-->
-- >--C-|-[ ]-|-D--> 
--      -------
--
-- (SyË¢ â‡’ Byá´¬) âŠ—â‡’ (Tyáµ— â‡’ Dyá¶œ) â‰¡ STyË¢áµ— â‡’ BDyáµƒá¶œ
_âŠ—â‡’_ : âˆ€{P Q R Z : Poly} â†’ P â‡’ Q â†’ R â‡’ Z â†’ P âŠ— R â‡’ Q âŠ— Z
(pâ‡’q âŠ—â‡’ râ‡’z) .map-base (ptag , rtag) = (map-base pâ‡’q ptag) , (map-base râ‡’z rtag)
(pâ‡’q âŠ—â‡’ râ‡’z) .map-fiber (ptag , rtag) (qargs , zargs) = (map-fiber pâ‡’q ptag qargs) , (map-fiber râ‡’z rtag zargs)

_â—â‡’_ : âˆ€{P Q R Z : Poly} â†’ P â‡’ Q â†’ R â‡’ Z â†’ P â— R â‡’ Q â— Z
(pâ‡’q â—â‡’ râ‡’z) .map-base (ptag , pargâ†’rtag) = (map-base pâ‡’q ptag) , Î» qargs â†’ map-base râ‡’z (pargâ†’rtag (map-fiber pâ‡’q ptag qargs))
(pâ‡’q â—â‡’ râ‡’z) .map-fiber (ptag , pargâ†’rtag) (qargs , zargs) = map-fiber pâ‡’q ptag qargs , map-fiber râ‡’z (pargâ†’rtag (map-fiber pâ‡’q ptag qargs)) zargs

_â“‹â‡’_ : âˆ€{P Q R Z : Poly} â†’ P â‡’ Q â†’ R â‡’ Z â†’ P â“‹  R â‡’ Q â“‹ Z
(pâ‡’q â“‹â‡’ râ‡’z) .map-base (ptag , rtag) = map-base pâ‡’q ptag , map-base râ‡’z rtag
(pâ‡’q â“‹â‡’ râ‡’z) .map-fiber (ptag , rtag) (injâ‚ qargs) = injâ‚ (map-fiber pâ‡’q ptag qargs)
(pâ‡’q â“‹â‡’ râ‡’z) .map-fiber (ptag , rtag) (injâ‚‚ (injâ‚ (qargs , zargs))) = injâ‚‚ (injâ‚ ((map-fiber pâ‡’q ptag qargs) , (map-fiber râ‡’z rtag zargs)))
(pâ‡’q â“‹â‡’ râ‡’z) .map-fiber (ptag , rtag) (injâ‚‚ (injâ‚‚ zargs)) = injâ‚‚ (injâ‚‚ (map-fiber râ‡’z rtag zargs))

_âˆ¨â‡’_ : âˆ€{P Q R Z : Poly} â†’ P â‡’ Q â†’ R â‡’ Z â†’ P âˆ¨ R â‡’ Q âˆ¨ Z
(pâ‡’q âˆ¨â‡’ râ‡’z) .map-base (injâ‚ ptag) = injâ‚ (map-base pâ‡’q ptag)
(pâ‡’q âˆ¨â‡’ râ‡’z) .map-base (injâ‚‚ (injâ‚ (ptag , rtag))) = injâ‚‚ (injâ‚ ((map-base pâ‡’q ptag) , (map-base râ‡’z rtag)))
(pâ‡’q âˆ¨â‡’ râ‡’z) .map-base (injâ‚‚ (injâ‚‚ rtag)) = injâ‚‚ (injâ‚‚ (map-base râ‡’z rtag))
(pâ‡’q âˆ¨â‡’ râ‡’z) .map-fiber (injâ‚ ptag) args = map-fiber pâ‡’q ptag args
(pâ‡’q âˆ¨â‡’ râ‡’z) .map-fiber (injâ‚‚ (injâ‚ (ptag , rtag))) (qargs , zargs) = (map-fiber pâ‡’q ptag qargs) , map-fiber râ‡’z rtag zargs
(pâ‡’q âˆ¨â‡’ râ‡’z) .map-fiber (injâ‚‚ (injâ‚‚ rtag)) args = map-fiber râ‡’z rtag args
